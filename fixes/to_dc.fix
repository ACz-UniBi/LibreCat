# mapping pub data to dublic core (dc)

copy_field(type, driver_type)
lookup(driver_type, fixes/lookup/driver_types.csv)
prepend(driver_type,'info:eu-repo/semantics/')
move_field(driver_type, 'res.type.$append')

copy_field(type, dini_type)
lookup(dini_type, fixes/lookup/dini_types.csv, default: other)
prepend(dini_type,'doc-type:')
move_field(dini_type, 'res.type.$append')
set_field('res.type.$append', text)

move_field(title,'res.title.$append')
move_field(alternative_title,'res.title.$append')

move_field(author.*.full_name,'res.creator.$append')
move_field(editor.*.full_name,'res.creator.$append')
move_field(corporate_editor,'res.creator.$append')

move_field(year,'res.date')
move_field(publisher,'res.publisher')

move_field(keyword,'res.subject')
move_field(citation.ama,'res.source.$append')

move_field(abstract.*.text, 'res.description.$append')

move_field(language.*.iso, 'res.language.$append')
lookup('res.language.*', fixes/oai_lang.csv)

prepend(doi,'doi/')
move_field(doi,'res.relation.$append')
prepend(publication_identifier.issn.*,'issn/')
move_field(publication_identifier.issn.*,'res.relation.$append')
prepend(publication_identifier.eissn.*,'issn/')
move_field(publication_identifier.eissn.*,'res.relation.$append')
prepend(publication_identifier.isbn.*,'isbn/')
move_field(publication_identifier.isbn.*,'res.relation.$append')
prepend(external_id.urn,'urn/')
move_field(external_id.urn,'res.relation.$append')
prepend(external_id.isi,'wos/')
move_field(external_id.isi,'res.relation.$append')
prepend(external_id.arxiv,'arxiv/')
move_field(external_id.arxiv,'res.relation.$append')
prepend(external_id.pmid,'pmid/')
move_field(external_id.pmid,'res.relation.$append')

prepend('res.relation.*','info:eu-repo/semantics/altIdentifier/')

prepend(external_id.fp6,'info:eu-repo/grantAgreement/EC/')
move_field(external_id.fp6,'res.relation.$append')

prepend(external_id.fp7,'info:eu-repo/grantAgreement/EC/')
move_field(external_id.fp7,'res.relation.$append')
#H2020 missing



# file stuff
unless exists(file)
  add_field('res.rights.$append','info:eu-repo/semantics/restrictedAccess')
end

do list(path => file)
  do identity()
    unless all_match(relation, hidden)
      move_field(content_type, 'res.format.$append')
      add_field(tmp,'https://pub.uni-bielefeld.de')
      add_field('tmp.$append',download)
      copy_field(_id,'tmp.$append')
      copy_field(file_id,'tmp.$append')
      join_field(tmp,'/')
      move_field(tmp,'res.identifier.$append')
      if any_match(access_level,open_access)
        add_field('res.rights.$append','info:eu-repo/semantics/openAccess')
      else
        add_field('res.rights.$append','info:eu-repo/semantics/restrictedAccess')
      end
    end
  end
end

retain_field(res)
move_field(res, .)
